ODROIDC-UBOOT-CONFIG

# video defaults
setenv m "1080p"
setenv vout_mode "hdmi"
setenv m_bpp "32"

# kernel args
setenv condev "console=ttyS0,115200n8 console=tty0"
setenv mmcargs "setenv bootargs 'root=${mmcroot} ${condev} no_console_suspend vdaccfg=0xa000 logo=osd1,loaded,0x7900000,720p,full dmfc=3 cvbsmode=576cvbs hdmimode=${m} m_bpp=${m_bpp} vout=${vout_mode}'"

# boot partition
setenv mmcdev "0"
setenv mmcpart "1"

# addresses
setenv loadaddr "0x21000000"
setenv initrd_addr "0x22000000"
setenv fdtaddr "0x21800000"

# load snappy system
fatload mmc ${mmcdev}:${mmcpart} ${loadaddr} snappy-system.txt
env import -t ${loadaddr} ${filesize}

# compatiblity hacks snappy vs u-boot
setenv kernel_file "uImage"
setenv initrd_file "uInitrd"
setenv loadkernel "fatload mmc ${mmcdev}:${mmcpart} ${loadaddr} ${snappy_ab}/${kernel_file}"
setenv loadinitrd "fatload mmc ${mmcdev}:${mmcpart} ${initrd_addr} ${snappy_ab}/${initrd_file}; setenv initrd_size ${filesize}"
setenv loadfdt "fatload mmc ${mmcdev}:${mmcpart} ${fdtaddr} ${snappy_ab}/dtbs/${fdtfile}"
setenv snappy_boot 'if test "${snappy_mode}" = "try"; then if fatexist mmc ${mmcdev}:${mmcpart} ${snappy_stamp}; then if test "${snappy_ab}" = "a"; then setenv snappy_ab "b"; else setenv snappy_ab "a"; fi; else fatwrite mmc ${mmcdev}:${mmcpart} 0x0 ${snappy_stamp} 0; fi; fi; run loadfiles; setenv mmcroot /dev/disk/by-label/system-${snappy_ab} ${snappy_cmdline}; run mmcargs; bootm ${loadaddr} ${initrd_addr}:${initrd_size} ${fdtaddr}'

# boot
run snappy_boot
